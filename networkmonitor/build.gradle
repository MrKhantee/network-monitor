/*
 * This source is part of the
 *      _____  ___   ____
 *  __ / / _ \/ _ | / __/___  _______ _
 * / // / , _/ __ |/ _/_/ _ \/ __/ _ `/
 * \___/_/|_/_/ |_/_/ (_)___/_/  \_, /
 *                              /___/
 * repository.
 *
 * Copyright (C) 2015-2017 Carmen Alvarez (c@rmen.ca)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'com.github.ben-manes.versions'

ext.googleSupportLibVersion = "27.0.2"

android {
    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    dataBinding {
        enabled = true
    }

    defaultConfig {
        applicationId "ca.rmen.android.networkmonitor"
        minSdkVersion 14
        targetSdkVersion 27
        versionName "1.30.1"
        versionCode 13001
        archivesBaseName += "-" + versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-project.txt'
        }
    }

    flavorDimensions "featureSet"

    productFlavors {
        foss {

        }
        proprietary {

        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {

    implementation 'de.psdev.licensesdialog:licensesdialog:1.8.3'
    implementation "com.android.support:appcompat-v7:${project.googleSupportLibVersion}"
    implementation "com.android.support:design:${project.googleSupportLibVersion}"
    implementation "com.android.support:preference-v7:${project.googleSupportLibVersion}"
    implementation "com.android.support:preference-v14:${project.googleSupportLibVersion}"
    implementation 'org.greenrobot:eventbus:3.0.0'
    implementation ('net.sourceforge.jexcelapi:jxl:2.6.12') {
        exclude group: "log4j"
    }
    implementation 'commons-net:commons-net:3.6'
    implementation 'com.github.hotchemi:permissionsdispatcher:3.1.0'
    annotationProcessor 'com.github.hotchemi:permissionsdispatcher-processor:3.1.0'

    if (!gradle.isFoss) {
        proprietaryImplementation 'com.google.android.gms:play-services-location:11.8.0'
    }

    implementation 'net.sourceforge.streamsupport:streamsupport:1.6.1'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'alpha-preview'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

configurations.all {
    resolutionStrategy {
        eachDependency { details ->
            if (details.requested.group == 'com.android.support'
                    && details.requested.name != 'multidex'
                    && details.requested.name != 'multidex-instrumentation') {
                details.useVersion project.googleSupportLibVersion
            }
        }
    }
}
